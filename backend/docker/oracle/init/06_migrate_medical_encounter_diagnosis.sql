DECLARE
    PROCEDURE exec_ddl_if_absent(p_check_sql IN VARCHAR2, p_ddl IN CLOB) IS
        v_cnt NUMBER;
    BEGIN
        EXECUTE IMMEDIATE p_check_sql INTO v_cnt;
        IF v_cnt = 0 THEN
            EXECUTE IMMEDIATE p_ddl;
        END IF;
    END;
BEGIN
    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_TABLES WHERE OWNER = ''CMH'' AND TABLE_NAME = ''MEDICAL_ENCOUNTER_DIAGNOSIS''',
        'CREATE TABLE CMH.MEDICAL_ENCOUNTER_DIAGNOSIS (
            ID NUMBER NOT NULL,
            ENCOUNTER_ID NUMBER NOT NULL,
            DIAGNOSIS_CODE VARCHAR2(100) NOT NULL,
            DIAGNOSIS_NAME VARCHAR2(300),
            IS_PRIMARY CHAR(1) NOT NULL,
            SORT_ORDER NUMBER NOT NULL,
            CREATED_BY VARCHAR2(50),
            CREATED_AT DATE NOT NULL
        )'
    );

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_CONSTRAINTS WHERE OWNER = ''CMH'' AND TABLE_NAME = ''MEDICAL_ENCOUNTER_DIAGNOSIS'' AND CONSTRAINT_NAME = ''PK_MED_ENC_DIAG''',
        'ALTER TABLE CMH.MEDICAL_ENCOUNTER_DIAGNOSIS ADD CONSTRAINT PK_MED_ENC_DIAG PRIMARY KEY (ID)'
    );

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_CONSTRAINTS WHERE OWNER = ''CMH'' AND TABLE_NAME = ''MEDICAL_ENCOUNTER_DIAGNOSIS'' AND CONSTRAINT_NAME = ''FK_MED_ENC_DIAG_ENC''',
        'ALTER TABLE CMH.MEDICAL_ENCOUNTER_DIAGNOSIS ADD CONSTRAINT FK_MED_ENC_DIAG_ENC FOREIGN KEY (ENCOUNTER_ID) REFERENCES CMH.MEDICAL_ENCOUNTER(ID)'
    );

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_CONSTRAINTS WHERE OWNER = ''CMH'' AND TABLE_NAME = ''MEDICAL_ENCOUNTER_DIAGNOSIS'' AND CONSTRAINT_NAME = ''CK_MED_ENC_DIAG_PRIMARY''',
        'ALTER TABLE CMH.MEDICAL_ENCOUNTER_DIAGNOSIS ADD CONSTRAINT CK_MED_ENC_DIAG_PRIMARY CHECK (IS_PRIMARY IN (''Y'',''N''))'
    );

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_SEQUENCES WHERE SEQUENCE_OWNER = ''CMH'' AND SEQUENCE_NAME = ''MEDICAL_ENC_DIAG_SEQ''',
        'CREATE SEQUENCE CMH.MEDICAL_ENC_DIAG_SEQ START WITH 2000 INCREMENT BY 1 NOCACHE'
    );

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_TRIGGERS WHERE OWNER = ''CMH'' AND TRIGGER_NAME = ''TRG_MED_ENC_DIAG_ID''',
        'CREATE OR REPLACE TRIGGER CMH.TRG_MED_ENC_DIAG_ID
         BEFORE INSERT ON CMH.MEDICAL_ENCOUNTER_DIAGNOSIS
         FOR EACH ROW
         WHEN (NEW.ID IS NULL)
         BEGIN
           :NEW.ID := CMH.MEDICAL_ENC_DIAG_SEQ.NEXTVAL;
         END;'
    );

    EXECUTE IMMEDIATE '
        INSERT INTO CMH.MEDICAL_ENCOUNTER_DIAGNOSIS (
            ENCOUNTER_ID,
            DIAGNOSIS_CODE,
            DIAGNOSIS_NAME,
            IS_PRIMARY,
            SORT_ORDER,
            CREATED_BY,
            CREATED_AT
        )
        SELECT e.ID,
               e.DIAGNOSIS_CODE,
               NULL,
               ''Y'',
               1,
               NVL(e.UPDATED_BY, e.CREATED_BY),
               NVL(e.UPDATED_AT, e.CREATED_AT)
          FROM CMH.MEDICAL_ENCOUNTER e
         WHERE e.DIAGNOSIS_CODE IS NOT NULL
           AND NOT EXISTS (
               SELECT 1
                 FROM CMH.MEDICAL_ENCOUNTER_DIAGNOSIS d
                WHERE d.ENCOUNTER_ID = e.ID
           )';
END;
/

DECLARE
    v_hospital_user_exists NUMBER;
BEGIN
    SELECT COUNT(*) INTO v_hospital_user_exists FROM ALL_USERS WHERE USERNAME = 'HOSPITAL';
    IF v_hospital_user_exists > 0 THEN
        EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON CMH.MEDICAL_ENCOUNTER_DIAGNOSIS TO HOSPITAL';
        EXECUTE IMMEDIATE 'GRANT SELECT ON CMH.MEDICAL_ENC_DIAG_SEQ TO HOSPITAL';
    END IF;
EXCEPTION
    WHEN OTHERS THEN
        IF SQLCODE IN (-1927, -1919) THEN NULL; ELSE RAISE; END IF;
END;
/
