DECLARE
    v_patient_table_exists NUMBER;

    PROCEDURE exec_ddl_if_absent(p_check_sql IN VARCHAR2, p_ddl IN CLOB) IS
        v_cnt NUMBER;
    BEGIN
        EXECUTE IMMEDIATE p_check_sql INTO v_cnt;
        IF v_cnt = 0 THEN
            EXECUTE IMMEDIATE p_ddl;
        END IF;
    END;
BEGIN
    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_TABLES WHERE OWNER = ''CMH'' AND TABLE_NAME = ''MEDICAL_ENCOUNTER_ASSET''',
        'CREATE TABLE CMH.MEDICAL_ENCOUNTER_ASSET (
            ID NUMBER NOT NULL,
            ENCOUNTER_ID NUMBER NOT NULL,
            PATIENT_ID NUMBER NOT NULL,
            ASSET_TYPE VARCHAR2(20) NOT NULL,
            TEMPLATE_CODE VARCHAR2(50),
            OBJECT_KEY VARCHAR2(500) NOT NULL,
            CREATED_BY VARCHAR2(50),
            CREATED_AT DATE NOT NULL
        )'
    );

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_CONSTRAINTS WHERE OWNER = ''CMH'' AND TABLE_NAME = ''MEDICAL_ENCOUNTER_ASSET'' AND CONSTRAINT_NAME = ''PK_MED_ENC_ASSET''',
        'ALTER TABLE CMH.MEDICAL_ENCOUNTER_ASSET ADD CONSTRAINT PK_MED_ENC_ASSET PRIMARY KEY (ID)'
    );

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_CONSTRAINTS WHERE OWNER = ''CMH'' AND TABLE_NAME = ''MEDICAL_ENCOUNTER_ASSET'' AND CONSTRAINT_NAME = ''FK_MED_ENC_AST_ENC''',
        'ALTER TABLE CMH.MEDICAL_ENCOUNTER_ASSET ADD CONSTRAINT FK_MED_ENC_AST_ENC FOREIGN KEY (ENCOUNTER_ID) REFERENCES CMH.MEDICAL_ENCOUNTER(ID)'
    );

    SELECT COUNT(*)
      INTO v_patient_table_exists
      FROM ALL_TABLES
     WHERE OWNER = 'CMH'
       AND TABLE_NAME = 'PATIENT';

    IF v_patient_table_exists > 0 THEN
        exec_ddl_if_absent(
            'SELECT COUNT(*) FROM ALL_CONSTRAINTS WHERE OWNER = ''CMH'' AND TABLE_NAME = ''MEDICAL_ENCOUNTER_ASSET'' AND CONSTRAINT_NAME = ''FK_MED_ENC_AST_PAT''',
            'ALTER TABLE CMH.MEDICAL_ENCOUNTER_ASSET ADD CONSTRAINT FK_MED_ENC_AST_PAT FOREIGN KEY (PATIENT_ID) REFERENCES CMH.PATIENT(PATIENT_ID)'
        );
    END IF;

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_CONSTRAINTS WHERE OWNER = ''CMH'' AND TABLE_NAME = ''MEDICAL_ENCOUNTER_ASSET'' AND CONSTRAINT_NAME = ''CK_MED_ENC_AST_TYPE''',
        'ALTER TABLE CMH.MEDICAL_ENCOUNTER_ASSET ADD CONSTRAINT CK_MED_ENC_AST_TYPE CHECK (ASSET_TYPE IN (''PEN'',''IMAGE''))'
    );

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_SEQUENCES WHERE SEQUENCE_OWNER = ''CMH'' AND SEQUENCE_NAME = ''MEDICAL_ENC_ASSET_SEQ''',
        'CREATE SEQUENCE CMH.MEDICAL_ENC_ASSET_SEQ START WITH 2000 INCREMENT BY 1 NOCACHE'
    );

    exec_ddl_if_absent(
        'SELECT COUNT(*) FROM ALL_TRIGGERS WHERE OWNER = ''CMH'' AND TRIGGER_NAME = ''TRG_MED_ENC_AST_ID''',
        'CREATE OR REPLACE TRIGGER CMH.TRG_MED_ENC_AST_ID
         BEFORE INSERT ON CMH.MEDICAL_ENCOUNTER_ASSET
         FOR EACH ROW
         WHEN (NEW.ID IS NULL)
         BEGIN
           :NEW.ID := CMH.MEDICAL_ENC_ASSET_SEQ.NEXTVAL;
         END;'
    );
END;
/

DECLARE
    v_hospital_user_exists NUMBER;
BEGIN
    SELECT COUNT(*) INTO v_hospital_user_exists FROM ALL_USERS WHERE USERNAME = 'HOSPITAL';
    IF v_hospital_user_exists > 0 THEN
        EXECUTE IMMEDIATE 'GRANT SELECT, INSERT, UPDATE, DELETE ON CMH.MEDICAL_ENCOUNTER_ASSET TO HOSPITAL';
        EXECUTE IMMEDIATE 'GRANT SELECT ON CMH.MEDICAL_ENC_ASSET_SEQ TO HOSPITAL';
    END IF;
EXCEPTION
    WHEN OTHERS THEN
        IF SQLCODE IN (-1927, -1919) THEN NULL; ELSE RAISE; END IF;
END;
/
